#[[
  MIT License

  Copyright (C) 2021-2023 by wangwenx190 (Yuhang Zhao)

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in
  all copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
]]

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

set(_@PROJECT_NAME@_supported_components Core Widgets Quick)
set(_@PROJECT_NAME@_available_components Core Gui)

foreach(_component ${@PROJECT_NAME@_FIND_COMPONENTS})
    if(_component IN_LIST _@PROJECT_NAME@_supported_components)
        set(__target @PROJECT_NAME@::${_component})
        if(TARGET ${__target})
            continue()
        else()
            set(__target_full @PROJECT_NAME@${_component})
            set(__targets_file "${CMAKE_CURRENT_LIST_DIR}/${__target_full}Targets.cmake")
            if(EXISTS "${__targets_file}")
                include("${__targets_file}")
                add_library(${__target} ALIAS @PROJECT_NAME@::${__target_full})
                list(APPEND _@PROJECT_NAME@_available_components ${_component})
            else()
                set(@PROJECT_NAME@_FOUND FALSE)
                set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "Can't find necessary configuration file for ${__target}, please make sure this component is built successfully and installed properly.")
                break()
            endif()
        endif()
    else()
        set(@PROJECT_NAME@_FOUND FALSE)
        set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "Unknown component: ${__target}.")
        break()
    endif()
endforeach()

if(NOT DEFINED @PROJECT_NAME@_FOUND)
    set(@PROJECT_NAME@_FOUND TRUE)
    set(@PROJECT_NAME@_VERSION "@PROJECT_VERSION@")
    set(@PROJECT_NAME@_VERSION_MAJOR "@PROJECT_VERSION_MAJOR@")
    set(@PROJECT_NAME@_VERSION_MINOR "@PROJECT_VERSION_MINOR@")
    set(@PROJECT_NAME@_VERSION_PATCH "@PROJECT_VERSION_PATCH@")
    #set(@PROJECT_NAME@_VERSION_TWEAK "@PROJECT_VERSION_TWEAK@")
    set(@PROJECT_NAME@_DESCRIPTION "@PROJECT_DESCRIPTION@")
    set(@PROJECT_NAME@_HOMEPAGE_URL "@PROJECT_HOMEPAGE_URL@")
    set(@PROJECT_NAME@_COMMIT "@PROJECT_VERSION_COMMIT@")
    set(@PROJECT_NAME@_COMPILE_DATETIME "@PROJECT_COMPILE_DATETIME@")
endif()

include(FeatureSummary)
set_package_properties(@PROJECT_NAME@ PROPERTIES
    DESCRIPTION "@PROJECT_DESCRIPTION@"
    URL "@PROJECT_HOMEPAGE_URL@"
)

if(${@PROJECT_NAME@_FOUND})
    list(REMOVE_DUPLICATES _@PROJECT_NAME@_available_components)
    find_dependency(QT NAMES Qt6 Qt5 REQUIRED COMPONENTS ${_@PROJECT_NAME@_available_components})
    find_dependency(Qt${QT_VERSION_MAJOR} REQUIRED COMPONENTS ${_@PROJECT_NAME@_available_components})
endif()
